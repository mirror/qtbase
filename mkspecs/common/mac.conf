#
# qmake configuration for common Mac OS X
#

QMAKE_PLATFORM += macx mac

QMAKE_RESOURCE		= /Developer/Tools/Rez

QMAKE_EXTENSION_SHLIB	= dylib

QMAKE_LIBDIR		=
QMAKE_INCDIR_OPENGL	= /System/Library/Frameworks/OpenGL.framework/Headers \
	/System/Library/Frameworks/AGL.framework/Headers/

QMAKE_FIX_RPATH         = install_name_tool -id 

QMAKE_LFLAGS_RPATH	=

QMAKE_LIBS_DYNLOAD	=
QMAKE_LIBS_OPENGL	= -framework OpenGL -framework AGL
QMAKE_LIBS_OPENGL_QT	= $$QMAKE_LIBS_OPENGL
QMAKE_LIBS_THREAD	=

QMAKE_AR		= ar cq
QMAKE_RANLIB		= ranlib -s

# Get path of Xcode's Developer directory
QMAKE_XCODE_DEVELOPER_PATH = $$system(xcode-select --print-path)
isEmpty(QMAKE_XCODE_DEVELOPER_PATH): \
    error("Xcode path is not set. Please use xcode-select to choose Xcode installation path")

# Make sure Xcode path is valid
!exists($$QMAKE_XCODE_DEVELOPER_PATH): \
    error("Xcode is not installed in $${QMAKE_XCODE_DEVELOPER_PATH}! Please use xcode-select to choose Xcode installation path")

include(unix.conf)
